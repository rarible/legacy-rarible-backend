archetype transfer_proxy(owner : address)

variable owner_candidate : option<address> = none

entry declare_ownership(candidate : address) {
  called by owner
  effect {
    owner_candidate := some(candidate);
  }
}

entry claim_ownership() {
  called by opt_get(owner_candidate)
  effect {
    owner := opt_get(owner_candidate);
    owner_candidate := none
  }
}

asset user {
  addr : address
}

entry add_user(new_user : address) {
  called by owner
  effect { user.add({ new_user }) }
}

entry rm_user(old_user : address) {
  called by owner
  effect { user.remove(old_user) }
}

entry do_transfers(arg : lambda<unit,list<operation>>) {
  called by user
  effect {
    operations := exec_lambda(arg, Unit)
  }
}
